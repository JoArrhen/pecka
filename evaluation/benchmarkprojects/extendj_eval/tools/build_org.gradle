plugins {
  id 'application'
  id 'signing'
  id 'maven-publish'

  // Applied in subprojects:
  id 'org.jastadd' version '1.15.0' apply false
}

repositories {
  mavenCentral()
}

dependencies {
  testImplementation 'junit:junit:4.12'
}

sourceSets {
  main.java {
    srcDir rootProject.file('src/frontend')
  }
}

defaultTasks 'jar'

group = 'org.extendj'
def fullVersion
if (hasProperty('newVersion')) {
  // A new version string should be saved.
  def props = new Properties()
  props['version'] = newVersion
  props.store(file('src/res/Version.properties').newWriter(), null)
  version = fullVersion = newVersion
} else {
  try {
    def proc = 'git describe'.execute(null, rootDir)
    if  (proc.waitFor() == 0) {
      fullVersion = proc.text.trim()
      // Trim to get latest tag:
      version = (fullVersion =~ /-\d+\-g.+$/).replaceAll('')
    } else {
      logger.warn('No git tags found.')
    }
  } catch (IOException e) {
    logger.warn("Failded to run git describe (${e.getMessage()}).")
  }
  if (!fullVersion?.trim()) {
    logger.warn('Reading stored version string.')
    def props = new Properties()
    file('src/res/Version.properties').withInputStream { stream -> props.load(stream) }
    version = fullVersion = props['version']
  }
}
println "Building ExtendJ version ${fullVersion}"

subprojects {
  apply plugin: 'application'
  apply plugin: 'java'
  apply plugin: 'org.jastadd'

  // Set the Java language level (-target -source).
  // This is configured in gradle.properties.
  sourceCompatibility = targetCompatibility = targetJavaVersion

  // Source encoding = UTF-8.
  compileJava.options.encoding = 'UTF-8'

  repositories {
    mavenLocal()
    flatDir { dirs "$rootDir/tools" }
  }
  dependencies {
    jastadd2 name: "jastadd2"
  }

  application {
    mainClass = 'org.extendj.JavaCompiler'
  }

  jar {
    archiveFileName = 'extendj.jar'
    manifest.attributes 'Main-Class': application.mainClass
    destinationDirectory = projectDir
  }


  // Copy version info from parent project.
  group = rootProject.group
  version = rootProject.version

  jastadd {
    configureModuleBuild()

    modules rootProject.file('jastadd_modules')
    // extraJastAddOptions = ["--cacheCycle=false"]
    astPackage = 'org.extendj.ast'
    genDir = 'src/gen'
    buildInfoDir = 'src/gen-res'
    parser.name = 'JavaParser'
    parser.genDir = 'src/gen/org/extendj/parser'
    scanner.genDir = 'src/gen/org/extendj/scanner'

    if (rootProject.hasProperty('extraJastAddOptions')) {
      extraJastAddOptions += rootProject.extraJastAddOptions.split(',') as List
      print("options: ${extraJastAddOptions}")
    }
  }

  task cleanGeneratedJava(type: Delete) {
    description 'Remove generated Java code.'
    delete file('src/gen')
  }

  task printClasspath {
    doLast {
        def classpath = sourceSets.main.compileClasspath.files
        def classpathString = classpath.collect { it.absolutePath }.join(':')
        println "Classpath used to compile the project:"
        println classpathString
    }
}

  task updateVersionString {
    outputs.upToDateWhen {
      def props = new Properties()
      def output = project.file('src/gen-res/Version.properties')
      if (output.isFile()) {
        output.withInputStream { stream -> props.load(stream) }
      }
      props['version'] == fullVersion
    }

    doLast {
      file('src/gen-res').mkdirs()
      def props = new Properties()
      props['version'] = fullVersion
      props.store(file('src/gen-res/Version.properties').newWriter(), null)
    }
  }

  clean.dependsOn cleanGeneratedJava
  processResources.dependsOn updateVersionString

  sourceSets {
    main.java {
      srcDir rootProject.file('src/backend-main')
      srcDir rootProject.file('src/frontend-main')
      srcDir 'src/gen'
    }
    main.resources {
      srcDir 'src/gen-res' // JastAdd buildInfo directory.
    }
  }

  java {
    withSourcesJar()
  }
}

// We generate this empty Jar to satisfy sonatype.
// We have our own generated documentation at https://extendj.org/doc2/
task emptyJavaDocJar(type: Jar) {
  classifier = 'javadoc'
}

afterEvaluate {
  // Only configure the artifact uploading if the OSSRH credentials are available.
  publishing {
    publications {
      mavenJava(MavenPublication) {
        artifactId = 'extendj'
        from components.java
        artifact emptyJavaDocJar

        versionMapping {
          usage('java-api') {
            fromResolutionOf('runtimeClasspath')
          }
          usage('java-runtime') {
            fromResolutionResult()
          }
        }

        pom {
          name = 'ExtendJ'
          description = 'Extensible Java Compiler.'
          url = 'https://extendj.org'
          licenses {
            license {
              name = 'Modified BSD License'
              url = 'http://opensource.org/licenses/BSD-3-Clause'
              distribution = 'repo'
            }
          }
          developers {
            developer {
              name = 'Torbjörn Ekman'
            }
            developer {
              name = 'Jesper Öqvist'
              email = 'jesper.oqvist@cs.lth.se'
            }
          }
          scm {
            connection = 'scm:git:https://bitbucket.org/extendj/extendj.git'
            url = 'https://bitbucket.org/extendj/extendj'
          }
        }
      }
    }

    if (project.hasProperty('ossrhUsername')) {
      repositories {
        maven {
          url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
          credentials {
            username = ossrhUsername
            password = ossrhPassword
          }
        }
      }
    }
  }

  signing {
    // Require keyId to sign:
    required { project.hasProperty('signing.keyId') }
    sign publishing.publications.mavenJava
  }
}
